/*******************************************************************************
 * Copyright (c) 2010, 2012 Institute for Dutch Lexicology
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *******************************************************************************/
/*

Corpus Query Language (CQL) parser definition.

javacc is used to generate Java code from this parser definition.

*/

options 
{
	STATIC=false;
	JAVA_UNICODE_ESCAPE=true;
	//USER_CHAR_STREAM=true;
}

PARSER_BEGIN(CorpusQueryLanguageParser)

package nl.inl.blacklab.queryParser.corpusql;

import java.util.Map;
import java.util.HashMap;
import nl.inl.blacklab.index.complex.ComplexFieldUtil;
import nl.inl.blacklab.search.TextPattern;
import nl.inl.blacklab.search.TextPatternAnd;
import nl.inl.blacklab.search.TextPatternEdge;
import nl.inl.blacklab.search.TextPatternNot;
import nl.inl.blacklab.search.TextPatternOr;
import nl.inl.blacklab.search.TextPatternPositionFilter;
import nl.inl.blacklab.search.TextPatternPositionFilter.Operation;
import nl.inl.blacklab.search.TextPatternProperty;
import nl.inl.blacklab.search.TextPatternRegex;
import nl.inl.blacklab.search.TextPatternTags;
import nl.inl.blacklab.search.TextPatternCaptureGroup;
import nl.inl.blacklab.search.indexstructure.IndexStructure;
import nl.inl.blacklab.search.sequences.TextPatternAnyToken;
import nl.inl.blacklab.search.sequences.TextPatternRepetition;
import nl.inl.blacklab.search.sequences.TextPatternSequence;

@SuppressWarnings("all")

public class CorpusQueryLanguageParser {

    public static void main(String[] args) throws nl.inl.blacklab.queryParser.corpusql.ParseException 
    {
		CorpusQueryLanguageParser parser = new CorpusQueryLanguageParser(new java.io.StringReader(args[0]));
		parser.query();
    }
    
    public static TextPattern parse(String query) throws nl.inl.blacklab.queryParser.corpusql.ParseException 
    {
        CorpusQueryLanguageParser parser = new CorpusQueryLanguageParser(new java.io.StringReader(query));
        return parser.query();
    }
    
    private int num(Token t)
    {
    	return Integer.parseInt(t.toString());
    }
    
    private String chopEnds(String input)
    {
    	if (input.length() >= 2)
    		return input.substring(1, input.length() - 1);
    	throw new RuntimeException();
    }
    
    private String getStringBetweenQuotes(String input) throws SingleQuotesException
    {
        if (!allowSingleQuotes && input.charAt(0) == '\'')
            throw new SingleQuotesException();
        return chopEnds(input);
    }
    
    private TextPattern simplePattern(String str)
    {
        if (str.length() > 0) {
            if (str.charAt(0) != '^')
                str = "^" + str;
            if (str.charAt(str.length() - 1) != '$')
                str += "$";
        }
        
        // Treat everything like regex now; will be simplified later if possible
        return new TextPatternRegex(str); 
    }

    /** Allow strings to be quoted using single quotes? */
    private boolean allowSingleQuotes = true;
    
    /** Allow strings to be quoted using single quotes? [default: yes] */
    public void setAllowSingleQuotes(boolean b) {
        allowSingleQuotes = b;
    }

    /** Allow strings to be quoted using single quotes? */
    public boolean getAllowSingleQuotes() {
        return allowSingleQuotes;
    }

    private String defaultProperty = "word"; //ComplexFieldUtil.DEFAULT_MAIN_PROP_NAME;
    
    public void setDefaultProperty(IndexStructure structure, String fieldName) {
        defaultProperty = structure.getComplexFieldDesc(fieldName).getMainProperty().getName();
    }

    public void setDefaultProperty(String property) {
        defaultProperty = property;
    }

    public String getDefaultProperty() {
        return defaultProperty;
    }

    private TextPattern propertyClause(String prop, TextPattern value) {
        // Main property has a name. Use that.
        if (prop == null || prop.length() == 0)
            prop = defaultProperty;
        return new TextPatternProperty(prop, value);
    }
    
}

PARSER_END(CorpusQueryLanguageParser)

// Skip whitespace and comment
SKIP:  
{
	" " 
|	"\t" 
|	"\n" 
|	"\r"
}

/* Comments are 'special tokens': they are not reported, but are available
   for use by the next reported token */
SPECIAL_TOKEN:
{
	<SINGLE_LINE_COMMENT: "#" (~["\n","\r"])* ("\n"|"\r"|"\r\n")? >
|	<MULTI_LINE_COMMENT: "/*" (~["*"] | "*" ~["/"])* "*/">
}

// Token definitions
TOKEN [IGNORE_CASE]:
{
    <WITHIN: "within" >
|   <CONTAINING: "containing" >
|	<NAME: ["A"-"Z","a"-"z","_"] (["A"-"Z","a"-"z","_","0"-"9"])* >
|	<NUMBER: (["0"-"9"])+ >
|	<QUOTED_STRING: "\"" (~["\"","\\"] | "\\" ~[] )* "\"" >
|   <SINGLE_QUOTED_STRING: "'" (~["'","\\"] | "\\" ~[] )* "'" >
}

// --- Grammar rules start here --- 

/* a query */
TextPattern query(): 
{
	TextPattern a = null;
} 
{
    a=complexQuery() <EOF>
    {
        return a;
    }
}

TextPattern complexQuery():
{
    TextPattern a = null, b = null;
    Token op = null;
    Operation oper = Operation.CONTAINING;
} 
{
    a=simpleQuery() [ op=queryOperator() b=complexQuery() ]
    {
        if (op == null)
            return a;
        if (op.toString().equals("within")) {
            // a within b
            oper = Operation.WITHIN;
        }
        
        return new TextPatternPositionFilter(a, b, oper);
    }
}

Token queryOperator():
{
    Token t = null;
}
{
    (t=<WITHIN> | t=<CONTAINING>)
    {
        return t;
    }
}

/* a simple query (not including containing, within, etc.) */
TextPattern simpleQuery(): 
{
    TextPattern query = null, rest = null;
    Token b = null;
} 
{
    query=sequence() [ b=booleanOperator() rest=simpleQuery() ]
    {
		if (b == null)
			return query;
        if (b.toString().equals("->"))
            throw new UnsupportedOperationException("Implication operator not yet supported");
		if (b.toString().equals("&"))
			return new TextPatternAnd(query, rest);
		return new TextPatternOr(query, rest);
    }
}

/* a sequence of token constraints */
TextPattern sequence(): 
{
	TextPattern first = null;
	TextPattern result = null;
	TextPattern[] tag = null;
    Token t2 = null;
} 
{
    first=sequencePart() [ LOOKAHEAD(2) result=sequenceRest(first) ]
	{
		if (result == null)
			result = first;
			
		return result;
	}
}

/* a start or self-closing tag tag such as <s> or <s/> (for finding XML elements).
 * Note that it returns one TextPattern, but if it's in [0] that means it's a start
 * tag; if it's in [1] it's a self-closing tag.
 */
TextPattern tag():
{
    Token tagName, endTagSlash = null, selfCloseSlash = null;
    Map<String, String> attr = new HashMap<String, String>();
}
{
    "<" (endTagSlash="/")? tagName=<NAME> ( attributes(attr) )* (selfCloseSlash="/")? ">"
    {
        if (endTagSlash != null && selfCloseSlash != null) {
            throw new MalformedTagException();
        }
        TextPattern tags = new TextPatternTags(tagName.toString(), attr);
        if (selfCloseSlash != null)
            return tags;
        if (endTagSlash != null) {
            if (attr.size() > 0) {
                throw new MalformedTagException("Cannot filter XML end tag on attributes");
            }
            return new TextPatternEdge(tags, true); // right edge
        }
        return new TextPatternEdge(tags, false); // left edge
    }
}

Token quotedString():
{
    Token a;
}
{
    a=<QUOTED_STRING>        { return a; }
|   a=<SINGLE_QUOTED_STRING> { return a; }
}

void attributes(Map<String, String> attr):
{
    Token name, value;
}
{
    name=<NAME> "=" value=quotedString()
    {
        attr.put(name.toString(), getStringBetweenQuotes(value.toString()));
    }
}

/* continuation of a sequence (after the first part) */
TextPattern sequenceRest(TextPattern first):
{
	Token op = null;
	TextPattern rest = null;
}
{
	rest=sequence()
	{
		if (rest == null)
			return first;
		return new TextPatternSequence(first, rest);
	}
}

/* a repetition specifier */
Integer[] repetitionAmount():
{
	Token t = null, t2 = null;
	Integer[] minMax = new Integer[2];
}
{
	"*"                                 { minMax[0] = 0; minMax[1] = -1; return minMax; }
|	"+"                                 { minMax[0] = 1; minMax[1] = -1; return minMax; }
|	"?"                                 { minMax[0] = 0; minMax[1] = 1; return minMax; }
|	LOOKAHEAD(3) "{" t=<NUMBER> "}"     { minMax[0] = minMax[1] = num(t); return minMax; }
|	"{" t=<NUMBER> "," [ t2=<NUMBER> ] "}"
	{
		minMax[0] = num(t);
		if (t2 == null)
			minMax[1] = -1;
		else
			minMax[1] = num(t2);
		return minMax; 
	}
}

/* boolean operator: and, or or implication */
Token booleanOperator():
{
	Token t = null;
}
{
	(t="&" | t="|" | t="->")  { return t; }
}

/* sequence part: one part in a sequence, possibly with a repetition operator */
TextPattern sequencePart(): 
{
	TextPattern m = null;
	Integer[] rep = null;
	Token captureName = null;
	Token captureNum = null;
}
{
	( ( captureName=<NAME> | captureNum=<NUMBER> ) ":")? ( m=tag() | m=position() | "(" m=complexQuery() ")" ) [ LOOKAHEAD(6) rep=repetitionAmount() ]
	{
		if (rep != null)
		{
            if (m instanceof TextPatternAnyToken)
            {
                TextPatternAnyToken any = (TextPatternAnyToken)m;
                m = any.repeat(rep[0], rep[1]);
            } else
                m = new TextPatternRepetition(m, rep[0], rep[1]);
		}
		if (captureName != null || captureNum != null) {
			String name = captureName != null ? captureName.toString() : captureNum.toString();
		    m = new TextPatternCaptureGroup(m, name);
		}
		return m;
	}
}

/* position: a simple attribute value or complex attribute expression between square brackets */
TextPattern position():
{
	TextPattern m = null;
}
{
	m=positionWord()         { return m; }
|	"[" [ m=positionLong() ] "]"
    {
        if (m == null)
            return new TextPatternAnyToken(1, 1);
        return m;
    }
}

/* position word: no brackets, just a single token constraint for the default attribute (usually word form) */
TextPattern positionWord():
{
	Token t = null;
}
{
	t=quotedString()      { return simplePattern(getStringBetweenQuotes(t.toString())); }
}

/* expression inside square brackets: an attribute clause, or multiple 
   attribute clauses combined with boolean operators */
TextPattern positionLong():
{
	TextPattern a = null, c = null;
	Token b = null;
}
{
	a=positionLongPart() [ b=booleanOperator() c=positionLong() ]
	{
		if (b == null)
			return a;
        if (b.toString().equals("->"))
            throw new UnsupportedOperationException("Implication operator not yet supported");
		if (b.toString().equals("&"))
			return new TextPatternAnd(a, c);
		return new TextPatternOr(a, c);
	}
}

/* attribute clause: an attribute/value pair, attribute expression between parens, or a negated attribute/value pair */
TextPattern positionLongPart():
{
	TextPattern m = null;
}
{
	LOOKAHEAD(3)
	m=attValuePair()              { return m; }
|	"(" m=positionLong() ")"      { return m; }
|	"!" m=positionLongPart()      { return new TextPatternNot(m); }
}

/* attribute/value pair, with equals or not equals sign, or possibly just a value (default attribute) */
TextPattern attValuePair():
{
	TextPattern v = null;
	String prop;
}
{
	LOOKAHEAD(2)
	prop=propName() "=" v=valuePart()   { return propertyClause(prop, v); }
|	prop=propName() "!=" v=valuePart()  { return new TextPatternNot(propertyClause(prop, v)); }
|	v=valuePart()                       { return v; }
}

String propName():
{
	Token t = null, subProp = null;
}
{
	t=<NAME> [ "/" subProp=<NAME> ]
	{
		String propName = t.toString(); 
		if (subProp == null)
			return propName;
		return propName + ":" + subProp.toString();
	}
}

/* value expression: a value or multiple values combined with boolean operators */
TextPattern value():
{
	TextPattern a = null, c = null;
	Token b = null;
}
{
	LOOKAHEAD(3) a=valuePart() b=booleanOperator() c=value()
	{
        if (b.toString().equals("->"))
            throw new UnsupportedOperationException("Implication operator not yet supported");
		if (b.toString().equals("&"))
			return new TextPatternAnd(a, c);
		return new TextPatternOr(a, c);
	}
|	a=valuePart()                   { return a; }
}

/* value: a quoted string, a value expression between parens, or a negated value */
TextPattern valuePart():
{
	TextPattern m = null;
	Token t = null;
}
{
	t=quotedString()              { return simplePattern(getStringBetweenQuotes(t.toString())); }
|	"(" m=value() ")"              { return m; }

}

